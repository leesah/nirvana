apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'io.fabric'
apply plugin: 'jacoco-android'

def versionProperties = new Properties()
versionProperties.load(new FileInputStream(rootProject.file('version.properties')))

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    defaultConfig {
        applicationId 'name.leesah.nirvana'
        minSdkVersion 24
        targetSdkVersion 27
        versionCode versionProperties['versionCode'].toInteger()
        versionName "SH${new Date().format('yyMMddHHmm')}"
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
        }
    }
    productFlavors {
    }
    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
            }
        }
    }
    sourceSets {
        String sharedTestDir = 'src/testCommon/java'
        test {
            java.srcDir sharedTestDir
        }
        androidTest {
            java.srcDir sharedTestDir
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    // Firebase
    implementation "com.google.firebase:firebase-core:$firebase_version"
    implementation "com.google.firebase:firebase-appindexing:$firebase_version"
    implementation "com.google.firebase:firebase-ads:$firebase_version"
    implementation ("com.crashlytics.sdk.android:crashlytics:2.7.1@aar") {transitive = true}

    // Android Support library
    implementation "com.android.support:appcompat-v7:$support_lib_version"
    implementation "com.android.support:design:$support_lib_version"
    implementation "com.android.support:cardview-v7:$support_lib_version"
    implementation "com.android.support.constraint:constraint-layout:1.0.2"
    androidTestImplementation "com.android.support:support-annotations:$support_lib_version"
    androidTestImplementation "com.android.support:recyclerview-v7:$support_lib_version"
    androidTestImplementation "com.android.support:design:$support_lib_version"

    // Testing
    testImplementation "junit:junit:$junit_version"
    testImplementation "org.mockito:mockito-core:$mockito_version"
    androidTestImplementation "junit:junit:$junit_version"
    androidTestImplementation "org.hamcrest:hamcrest-library:1.4-atlassian-1"
    androidTestImplementation "org.mockito:mockito-android:$mockito_version"

    androidTestImplementation "com.android.support.test:runner:1.0.2-alpha1"
    androidTestImplementation "com.android.support.test:rules:1.0.2-alpha1"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$espresso_version"
    androidTestImplementation "com.android.support.test.espresso:espresso-contrib:$espresso_version"

    testImplementation "org.robolectric:robolectric:$robolectric_version"

    // Utility libraries
    implementation "com.google.code.gson:gson:2.8.1"
    implementation "joda-time:joda-time:2.9.9"
    implementation "org.apache.commons:commons-lang3:3.6"
    implementation "com.google.guava:guava:23.0"
    testImplementation "com.google.guava:guava:23.0"
    androidTestImplementation "com.google.guava:guava:23.0"
    implementation "com.google.code.findbugs:jsr305:3.0.2"
    androidTestImplementation "com.google.code.findbugs:jsr305:3.0.2"
}

jacoco {
    toolVersion = "0.7.9"
}

jacocoAndroidUnitTestReport {
    csv.enabled false
    html.enabled false
    xml.enabled true
}

apply plugin: 'com.google.gms.google-services'
repositories {
    mavenCentral()
}
