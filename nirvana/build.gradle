import groovy.time.TimeCategory

apply plugin: 'com.android.application'
apply plugin: 'com.github.triplet.play'

def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(rootProject.file('keystore.properties')))

def DAY0 = Date.parse('yyyy-MM-dd', '2017-04-07')

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'
    playAccountConfigs {
        defaultAccountConfig {
            jsonFile = file('play-publish-auth-key.json')
        }
    }
    defaultConfig {
        applicationId 'name.leesah.nirvana'
        minSdkVersion 24
        targetSdkVersion 25
        versionCode use(TimeCategory) {(new Date() - DAY0).days}
        versionName sprintf('%s%d', 'HRB', versionCode)
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        playAccountConfig = playAccountConfigs.defaultAccountConfig
    }
    signingConfigs {
        config {
            storeFile rootProject.file('keystore/release.jks')
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
        debug {
            signingConfig signingConfigs.config
        }
    }
    productFlavors {
    }
    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }
    sourceSets {
        String sharedTestDir = 'src/testCommon/java'
        test {
            java.srcDir sharedTestDir
        }
        androidTest {
            java.srcDir sharedTestDir
        }
    }
}

play {
    track = 'alpha'
    userFraction = 1
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support:cardview-v7:25.3.1'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.google.code.gson:gson:2.8.0'
    compile 'joda-time:joda-time:2.9.6'
    compile 'org.apache.commons:commons-lang3:3.5'
    compile 'org.parceler:parceler-api:1.1.6'
    annotationProcessor 'org.parceler:parceler:1.1.6'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.2.28'
    testCompile 'org.robolectric:robolectric:3.3.2'
    testCompile 'com.google.guava:guava:21.0'
    androidTestCompile 'junit:junit:4.12'
    androidTestCompile 'org.hamcrest:hamcrest-library:1.3'
    androidTestCompile 'org.mockito:mockito-android:2.7.21'
    androidTestCompile 'com.google.guava:guava:21.0'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.2'
    androidTestCompile 'com.android.support.test.espresso:espresso-contrib:2.2.2'
    androidTestCompile 'com.android.support:support-annotations:25.3.1'
    androidTestCompile 'com.android.support:recyclerview-v7:25.3.1'
    androidTestCompile 'com.android.support:design:25.3.1'
}
