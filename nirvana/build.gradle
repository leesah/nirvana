apply plugin: 'com.android.application'
apply plugin: 'com.github.triplet.play'

def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(rootProject.file('keystore.properties')))

android {
    compileSdkVersion 'android-O'
    buildToolsVersion '26.0.0 rc1'
    playAccountConfigs {
        defaultAccountConfig {
            jsonFile = file('play-publish-auth-key.json')
        }
    }
    defaultConfig {
        applicationId 'name.leesah.nirvana'
        minSdkVersion 24
        targetSdkVersion 26
        versionCode 32
        versionName sprintf('HRB%d', versionCode)
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        playAccountConfig = playAccountConfigs.defaultAccountConfig
    }
    signingConfigs {
        config {
            storeFile rootProject.file('keystore/release.jks')
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
        debug {
            signingConfig signingConfigs.config
        }
    }
    productFlavors {
    }
    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }
    sourceSets {
        String sharedTestDir = 'src/testCommon/java'
        test {
            java.srcDir sharedTestDir
        }
        androidTest {
            java.srcDir sharedTestDir
        }
    }
}

play {
    track = 'alpha'
    userFraction = 1
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile "com.android.support:appcompat-v7:$rootProject.ext.support_lib_version"
    compile "com.android.support:design:$rootProject.ext.support_lib_version"
    compile "com.android.support:cardview-v7:$rootProject.ext.support_lib_version"
    compile "com.android.support.constraint:constraint-layout:1.0.2"
    compile "com.google.code.gson:gson:2.8.0"
    compile "com.google.guava:guava:$rootProject.ext.guava_version"
    compile "joda-time:joda-time:2.9.9"
    compile "org.apache.commons:commons-lang3:3.5"
    testCompile "junit:junit:$rootProject.ext.junit_version"
    testCompile "org.mockito:mockito-core:$rootProject.ext.mockito_version"
    testCompile "org.robolectric:robolectric:3.3.2"
    testCompile "com.google.guava:guava:$rootProject.ext.guava_version"
    androidTestCompile "junit:junit:$rootProject.ext.junit_version"
    androidTestCompile "org.hamcrest:hamcrest-library:1.3"
    androidTestCompile "org.mockito:mockito-android:$rootProject.ext.mockito_version"
    androidTestCompile "com.google.guava:guava:$rootProject.ext.guava_version"
    androidTestCompile "com.android.support.test:runner:0.5"
    androidTestCompile "com.android.support.test:rules:0.5"
    androidTestCompile "com.android.support.test.espresso:espresso-core:$rootProject.ext.espresso_version"
    androidTestCompile "com.android.support.test.espresso:espresso-contrib:$rootProject.ext.espresso_version"
    androidTestCompile "com.android.support:support-annotations:$rootProject.ext.support_lib_version"
    androidTestCompile "com.android.support:recyclerview-v7:$rootProject.ext.support_lib_version"
    androidTestCompile "com.android.support:design:$rootProject.ext.support_lib_version"
    compile 'com.google.firebase:firebase-core:10.2.4'
    compile 'com.google.firebase:firebase-crash:10.2.4'
    compile 'com.google.firebase:firebase-appindexing:10.2.4'
}

apply plugin: 'com.google.gms.google-services'